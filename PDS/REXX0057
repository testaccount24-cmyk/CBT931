/* REXX REXX0057  - SUBJCL3
   SUBMITTING JCL FOR BATCH PROCESSING.
   INSERTING VARIABLE VALUES INTO THE JCL.
   USING THE TSO LINE MODE EDITOR TO CREATE
   A TEMPORARY FILE, SUBMIT IT,
   AND EXIT WITHOUT SAVING IT
*/
/* TRACE C */
TEMP_FILE_NAME = "'"USERID()".TEMP.SUBMIT.CNTL'"
PROGRAM_TO_EXECUTE = "MYPROG1"
JCL_DELIMITER = "/*"
/* DELETE, IF IT EXISTS. IT'S A TEMP FILE.
   YOU SHOULD HAVE NOTHING GOOD IN A TEMP FILE. GOODBYE.
*/
CALL MSG "OFF"
"DELETE" TEMP_FILE_NAME
CALL MSG "ON"

"ALLOCATE DSN("TEMP_FILE_NAME") NEW REUSE TRACKS",
   "SPACE(3,1) LRECL(80) RECFM(F,B) BLKSIZE(8000)"

QUEUE "10 //"USERID()"A JOB (0),'TEST JOB',"
QUEUE "20 //         TYPRUN=SCAN,  "
QUEUE "30 //         MSGLEVEL=1,CLASS=A,NOTIFY="USERID()
QUEUE "40 //STEP1    EXEC PGM="PROGRAM_TO_EXECUTE
QUEUE "50 //INFILE   DD DSN="USERID()".INPUT.FILE,DISP=SHR "
QUEUE "60 //OUTFILE  DD SYSOUT=A "
QUEUE "70 //SYSIN    DD * "
QUEUE "80" DATE() TIME()
QUEUE "90" JCL_DELIMITER
QUEUE "LIST"
QUEUE "SUBMIT"
QUEUE "END NOSAVE"
"EDIT" TEMP_FILE_NAME "CNTL OLD   NUM EMODE"
"STATUS"
